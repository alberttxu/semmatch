ScriptName findHoles

# If something goes wrong, set Debug = 1
Debug = 0              # True = 1 ; False = 0

### semmatch arguments
param2 = 40         # hough threshold
maxPts = 999         # return subset of all found points

acquire = 1                                    # True = 1 ; False = 0

# names of temporary files used by semmatch
outputNav = semmatch_nav.nav
image = MMM.jpg

ReportIfNavOpen
If $reportedValue1 != 2
   Exit
Endif
ReportNavFile 1
navfile = $reportedValue1$reportedValue2
navdir = $reportedValue3
SetDirectory $navdir
SaveNavigator

If $acquire != 1 AND $acquire != 0
   Echo acquire should be either 1 or 0
   Exit
Endif

If $Debug == 1
   debugStr = /k
ElseIf $Debug == 0
   debugStr = /c
Else
   Echo Debug should be either 1 or 0
   Exit
Endif

## load and bin MMM map
ReportNavItem
If $RepVal5 != 2        # if not a map item
   Echo Not a map item. Select a Map item from the navigator.
   Exit
Endif
MAP = $navLabel
Echo Map Label: $MAP
SetUserSetting BufferToReadInto 16
SetUserSetting LoadMapsUnbinned 1
# uncheck Montage Controls "Align pieces in overview"
ReportUserSetting MontageAlignPieces alignChecked
If $alignChecked == 1
   SetUserSetting MontageAlignPieces 0
Endif
LoadNavMap

# reduce image if original pixelsize < 40 nm
normalizedPixelsize = 40
ImageProperties Q
origPixelsize = $repVal5

If $origPixelsize < $normalizedPixelsize
   reduction = $normalizedPixelsize / $origPixelsize
   ReduceImage Q $reduction
Else
   Copy Q A
   reduction = 1
Endif
Show Q

ImageProperties A
pixelsize = $repVal5

## make a jpeg image
SaveToOtherFile A JPG JPG $image
Echo saved $image

ReportOtherItem -1
newLabel = $navIntLabel + 1

RunInShell cmd $debugStr "conda activate semmatch && \
semmatch \
--navfile $navfile \
--mapLabel $MAP \
--newLabel $newLabel \
--output $outputNav \
--acquire $acquire \
--image $image \
--pixelsize $pixelsize \
findHoles --reduction $reduction --maxPts $maxPts --param2 $param2 "

MergeNavFile $outputNav
If $alignChecked == 1
   SetUserSetting MontageAlignPieces 1
Endif
Show Q
