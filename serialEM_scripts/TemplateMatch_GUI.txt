ScriptName TemplateMatch_GUI

### Before running this script, make sure:
#   1.  The working directory is set to where the navigator file is.
#           This can be done via:
#              Menubar->Script->Edit/Run one Line
#              In any of the 5 prompts, type UserSetDirectory and click Run.
#              Select the folder containing the navigator file.
#   2.  Save a template of a hole/pattern as a jpg image.
#         Crop a hole using ctrl+shift+drag,
#             and then select Menubar->Process-> Crop Image.
#         Then using the Edit/Run one Line prompt, run
#             SaveToOtherFile A JPG JPG T.jpg
#

Debug = 0              # True = 1 ; False = 0

### semmatch arguments
threshold = 0.8
currentNav = nav.nav
outputNav = newNav.nav
image = MMM.jpg
template = T.jpg
acquire = 1                                    # True = 1 ; False = 0
groupOption = 2
    #    0 = no groups
    #    1 = groups based on radius
    #    2 = all points as one group
    #    3 = specify a certain number of groups
    #    4 = specify number of points per group

If $acquire != 1 AND $acquire != 0
   Echo acquire should be either 1 or 0
   Exit
Endif

If $Debug == 1
   debugStr = /k
ElseIf $Debug == 0
   debugStr = /c
Else
   Echo Debug should be either 1 or 0
   Exit
Endif

## load and bin MMM map
ReportNavItem
If $RepVal5 != 2        # if not a map item
   Echo -> Not a map item, Exiting
   Exit
Endif
MAP = $navLabel
Echo Map Label: $MAP
SetUserSetting BufferToReadInto 16
SetUserSetting LoadMapsUnbinned 1
LoadNavMap

# reduce image if larger than 2000x2000
maxdimLimit = 2000
ImageProperties Q width height
maxdim = $width
If $width < $height
   maxdim = $height
Endif

If $maxdim < $maxdimLimit
   Copy Q A
   reduction = 1
Else
   reduction = $maxdim / $maxdimLimit
   ReduceImage Q $reduction
Endif
Show Q

## make a jpeg image
SaveToOtherFile A JPG JPG $image
Echo saved $image

ReportOtherItem -1
newLabel = $navIntLabel + 1

RunInShell cmd $debugStr " "semmatch" \
"--gui" \
"--navfile" "$currentNav" \
"--reduction" "$reduction" \
"--image" "$image" \
"--template" "$template" \
"--mapLabel" "$MAP" \
"--newLabel" "$newLabel" \
"--threshold" "$threshold" \
"--groupOption" "$groupOption" \
"--output" "$outputNav" \
"--noBlurTemplate" \
"--noBlurImage" \
"--acquire" "$acquire" "

MergeNavFile $outputNav
Show Q

